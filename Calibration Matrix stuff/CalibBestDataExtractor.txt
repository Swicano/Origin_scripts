// This script comes from 'Calib test 2p2 peak fitting.opj' 
//  
//  it does a lot less. having already generated the previous data
//  it takes an external sheet with requirements (min,max wavelength,R,slope,etc)
//  and finds the wavelength pair that maximizes slope, or R^2, or if we want to 
//  look at one specific wavelength pair. it extracts that data into a traditional 
//  spreadsheet two column (temperature vs ratio) sheet, and plots the data as well
//  as the associated fit



// when starting, set these values lower 
// than the final value to speed up test runs

int runsPer = 5;  	  // number of datasets (x,y pairs) in each datasheet, or: how many columns of Y data
int totSheets = 18;	  //35 // total number of sheets of data 35
int dataLength = 1550;//1550;  //3648	// how many rows of Y data on each sheet
int dataStartRow = 740; // usually 1 but in case you want to skip some early data like saya  filter
int offsetX = 1;   // where you start your array, default is 1 ( in the resulting matrix, the X direction is top to bottom (aka X offset is ROW offset))
int offsetY = 5;   //where you start your array, default is 1
int dataSkip = 5;  // default is 1 for no skip

// these values don't affect the calculation time
// but are used to label the graphs correctly 
int startWaveLen = 500;
int endWaveLen = 800;

range inputWaves = [Book1]"28C"!col(A);


stringarray datasheets;           // the sheet name of the input sheets, each temperature
datasheets.Add("28C"); //1   // gets its own sheet and all sheets need the same number of runs,
datasheets.Add("41C");       // so add more at the end as needed.
datasheets.Add("45C");       // these numbers just help keep track of the total sheets
datasheets.Add("64C");
datasheets.Add("83C"); //5
datasheets.Add("102C"); 
datasheets.Add("128C");
datasheets.Add("22C");
datasheets.Add("29C");
datasheets.Add("39C"); //10
datasheets.Add("45C1"); 
datasheets.Add("48C");
datasheets.Add("68C");
datasheets.Add("75C");
datasheets.Add("88C"); //15
datasheets.Add("102C1"); 
datasheets.Add("125C");
datasheets.Add("143C");
//datasheets.Add("C");
//datasheets.Add("C"); //20
//datasheets.Add("s3 75C"); 
//datasheets.Add("s3 100C");
//datasheets.Add("s3 110C");
//datasheets.Add("s3 120C");
//datasheets.Add("s3 130C"); //25
//datasheets.Add("s3 150C");
//datasheets.Add("s4 20C");
//datasheets.Add("s4 50C 1");
//datasheets.Add("s4 50C 2");
//datasheets.Add("s4 100C 1"); //30
//datasheets.Add("s4 100C 2");
//datasheets.Add("s4 150C 1");
//datasheets.Add("s4 150C 2");
//datasheets.Add("s4 200C 1");
//datasheets.Add("s4 200C 2"); //35


stringarray datatemps;
datatemps.Add("28");//1     // the temperature associated with each sheet, 
datatemps.Add("41");        // duplicates are OK, add more at the end as needed
datatemps.Add("45");
datatemps.Add("64");
datatemps.Add("83");//5
datatemps.Add("102");
datatemps.Add("128");
datatemps.Add("22");
datatemps.Add("29");
datatemps.Add("39");//10
datatemps.Add("45");
datatemps.Add("48");
datatemps.Add("68");
datatemps.Add("75");
datatemps.Add("88");//15
datatemps.Add("102");
datatemps.Add("125");
datatemps.Add("143");
//datatemps.Add("20");
//datatemps.Add("50");//20
//datatemps.Add("75");
//datatemps.Add("100");
//datatemps.Add("110");
//datatemps.Add("120");
//datatemps.Add("130"); //25
//datatemps.Add("150");
//datatemps.Add("20");
//datatemps.Add("50");
//datatemps.Add("50");
//datatemps.Add("100"); //30
//datatemps.Add("100");
//datatemps.Add("150");
//datatemps.Add("150");
//datatemps.Add("200");
//datatemps.Add("200"); //35


stringarray datacols;     // This describes which columns in each sheet hold the Y data, 
datacols.Add("B");        // add more at the end as appropiate.
datacols.Add("D");
datacols.Add("F");
datacols.Add("H");
datacols.Add("J");

 

// heres our list of inputs that act as cutoffs
range sliceReqs = [Book2]Sheet1!B; 
double minR2 = sliceReqs[1];
double minB = sliceReqs[2];
double minWL = sliceReqs[3]-startWaveLen;
double maxWL = sliceReqs[4]-startWaveLen;
int inNumer = sliceReqs[5]-dataStartRow;
int inDenom = sliceReqs[6]-dataStartRow;

// we want to loop over the R^2 Matrix and find the largest R^2, later we will add a set of restrictions as we see need for
int BestRNumer = 0;
int BestRDenom = 0;
double BestRslop = 0;
double BestRint = 0;
double BestR2val = 0;
int BestBNumer = 0;
int BestBDenom = 0;
double BestBfit = 0;
double BestBint = 0;
double BestBval = 0;

string linfit$ = "Fit";
range fitA = [MBook1]linfit$!1;
range fitB = [MBook1]linfit$!2;
range fitR = [MBook1]linfit$!3;
range fitR2 = [MBook1]linfit$!4;
range sumxx = [MBook1]linfit$!5;
range sumxy = [MBook1]linfit$!6;
range sumx = [MBook1]linfit$!7;
range sumy = [MBook1]linfit$!8;
range xbar = [MBook1]linfit$!9;
range ybar = [MBook1]linfit$!10;
range xdev = [MBook1]linfit$!11;
range xydev = [MBook1]linfit$!12;
range xxdev = [MBook1]linfit$!13;
range yydev = [MBook1]linfit$!14;
range goodpoints = [MBook1]linfit$!15; // to keep track of how many points we used in each position?

loop(ij,1,dataLength) // number of rows per data run
{
  loop(jj,1,dataLength) // number of rows per data run
  {
    if( (ij >minWL) & (jj >minWL) & (ij<maxWL) & (jj<maxWL) & (fitR2[ij,jj] > minR2) & (fitB[ij,jj]>minB)& ( ij!=jj) )// here is where we would add other restrictions as necessary
    {
      if (fitR2[ij,jj] > BestR2val) // this IF is for the R^2
      {
        BestR2val = fitR2[ij,jj];
        BestRNumer = ij;
        BestRDenom = jj; 
        BestRslop = fitB[ij,jj];
        BestRint = fitA[ij,jj];
      }
      if (abs(fitB[ij,jj]) > BestBval) // this IF is for the B
      {
        BestBval = fitB[ij,jj];
        BestBNumer = ij;
        BestBDenom = jj;
        BestBfit = fitR2[ij,jj];
        BestBint = fitA[ij,jj];
      }
    }
  }
}


// lastly we will extract the Ratio Vs Temp graph for each
range sliceTemp = [Book2]Sheet1!C; 
range sliceR2 = [Book2]Sheet1!D;     // the data
range sliceR2fit = [Book2]Sheet1!E;  // the associated linear fit
range sliceB = [Book2]Sheet1!F;      // the data
range sliceBfit = [Book2]Sheet1!G;   // the associated fit
range sliceChose = [Book2]Sheet1!H;      // the data
range sliceChosefit = [Book2]Sheet1!I;   // the associated fit
string ratioR2    = Ratio of $(BestRNumer) / $(BestRDenom);
string ratioB     = Ratio of $(BestBNumer) / $(BestBDenom);
string ratioChose = Ratio of $(inNumer) / $(inDenom);
sliceTemp[L]$ = Temperature;
sliceR2[L]$ = ratioR2$;
sliceB[L]$ = ratioB$;
sliceChose[L]$ = ratioChose$;

sliceR2fit[L]$ = ratioR2$;
sliceBfit[L]$ = ratioB$;
sliceChosefit[L]$ = ratioChose$;
sliceTemp[U]$ = "C";
//sliceR2[U]$ = "";
//sliceB[U]$ = "";
//sliceWLen[C]$ = "";

sliceR2[C]$ = $(inputWaves[BestRNumer+dataStartRow]) / $(inputWaves[BestRDenom+dataStartRow]) : best R^2: $(BestR2val,%5f) w/ $(BestRslop,%5f) Slope;
sliceB[C]$ = $(inputWaves[BestBNumer+dataStartRow]) / $(inputWaves[BestBDenom+dataStartRow]) : best Slope: $(BestBval,%5f) w/ $(BestBfit,%5f) R2;
sliceChose[C]$ = $(inputWaves[inNumer+dataStartRow]) / $(inputWaves[inDenom+dataStartRow]) :User Choice;
sliceR2fit[C]$ = Fit line for R2;
sliceBfit[C]$ = Fit line for Slope;
sliceChosefit[C]$ = Fit line for User Choice;
int counter = 1;
loop(sheetnum,1,totSheets)
{
  string currentdatasheet$ = datasheets.GetAt(sheetnum)$;
  string strTemp$ = datatemps.GetAt(sheetnum)$;
  int temp = %(strTemp$);
  
  loop(m,1,runsPer)
  {
    range mn = [MBook1]currentdatasheet$!$(m);
    sliceTemp[counter] = temp;
    sliceR2[counter] = mn[BestRNumer,BestRDenom];
    sliceR2fit[counter] = (temp)*BestRslop+BestRint;
    sliceB[counter] = mn[BestBNumer,BestBDenom];
    sliceBfit[counter] = (temp)*BestBval+BestBint;
    sliceChose[counter] = mn[inNumer,inDenom];
    sliceChosefit[counter] = (temp)*fitB[inNumer,inDenom]+fitA[inNumer,inDenom];
    counter = counter+1;
  }
}

window -e Graph9;
Plotxy iy:=((sliceTemp,sliceR2),(sliceTemp,sliceB)) plot:=201 ogl:=[Graph9]!1;
Plotxy iy:=((sliceTemp,sliceR2fit),(sliceTemp,sliceBfit)) plot:=200 ogl:=[Graph9]!2;